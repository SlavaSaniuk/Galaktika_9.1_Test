//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Логистика
// форма ТН-2. Бизнес-текст
//******************************************************************************

#define _TN2_PC_FRN_
#define USE_SEP_SYMBOL
#ifdef _CURRENT_FORM_

#ifdef _PROCNAC_
#ifdef _TTN_MAIN_
#doc
Накладные на продажу
ТН-2 (2009 в руб.)
#end
.linkform 'ttn2 (2009)' prototype is 'TOVN'
.NameInList 'ТН-2 (2009 в руб.)'
.group 'Накладные на продажу'
.group 'Накладные на продажу (fast)'
.group 'Акты на строительство'
.group 'Акты на услуги'
.group 'Акт на перемещение между объектами'
#end
#ifdef _TTN_DOC_600_
#doc
ТТН_600
ТН-2 (2009 в руб.)
#end
.linkform 'ttn2 (2009)_600' prototype is 'TOVN'
.NameInList 'ТН-2 (2009 в руб.)'
.group 'ТТН_600'
.group 'ТТН_600 (fast)'
#end
#ifdef _TTN_AKTREC_
#doc
Рекламационные накладные
ТН-2 (2009 в руб.)
#end
.linkform 'ttn2 (2009)_AKTREC' prototype is 'TOVN'
.NameInList 'ТН-2 (2009 в руб.)'
.group 'Рекламационные накладные'
#end
#ifdef _TTN_DOC_ROZN_201_
#doc
Розничные накладные
ТН-2 (2009 в руб.)
#end
.LinkForm 'ttn2 (2009)_RZ919' prototype is 'TOVN'
.NameInList 'ТН-2 (2009 в руб.)'
.group 'Розничные накладные'
.group 'Розничные накладные (fast)'
#end
#ifdef _TTN_DOC_ROZN_600_
#doc
Розничные накладные_600
ТН-2 (2009 в руб.)
#end
.LinkForm 'ttn2 (2009)_RZ908' prototype is 'TOVN'
.NameInList 'ТН-2 (2009 в руб.)'
.group 'Розничные накладные_600'
#end
#include format.frn
#include summa.frn
#include ttn_fun.frn
#include ttn_094.frn
#include org_decl.frn
#include mfo.frn
#include ttnf_var.frn
.defo PORTRAIT
.P 71
#include ttn_var_09.frn
.fields
//******************************************************************************
// ШАПКА НАКЛАДНОЙ
  // Грузоотправитель - код УНП
  oPrnSoprFun.GetOrgUNN(Грузоотправитель, ГрузоотправительУНН, ПоставщикУНН)

  // Грузополучатель - код УНП
  oPrnSoprFun.GetOrgUNN(Грузополучатель, ГрузополучательУНН, ПоставщикУНН)

  // Дата создания накладной - день
  ДатаДень

  // Дата создания накладной - месяц
  ДатаМесяц

  // Дата создания накладной - год
  ДатаГод

  // Номер накладной
  Номер

  // Грузоотправитель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 1, 0)
    + oPrnSoprFun.TTNBelSelfAddr (Грузоотправитель, Грузоотправитель_ЮрАдрес, Грузоотправитель_Адрес, Поставщик, Поставщик_Адрес, Склад, Склад_Адрес, 1)

  // Грузоотправитель - наименование головной организации
#ifndef _TTN_2009F_
  + ' ' +
#end
  oPrnSoprFun.GetParentOrg(Грузоотправитель_Головная_Орг_Наимен)

  // Грузополучатель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 2, 0)
    + oPrnSoprFun.TTNBelAddr (Грузополучатель, Грузополучатель_ЮрАдрес, Грузополучатель_Адрес, Поставщик, Поставщик_Адрес, Склад_Пол, Склад_Пол_Адрес, 1)

  // Грузополучатель - наименование головной организации
#ifndef _TTN_2009F_
  + ' ' +
#end
  oPrnSoprFun.GetParentOrg(Грузополучатель_Головная_Орг_Наимен)

  // Основание отпуска
  oPrnSoprFun.GetOsnovOtp(Дата_Договора, ДатаОснования, Номер_Договора, Основание, Основание_отпуска, Цель_приобретения)
//******************************************************************************



//******************************************************************************
// МНОГОСТРАНИЧНАЯ НАКЛАДНАЯ. ВЕРХНИЙ КОЛОНТИТУЛ
//******************************************************************************

  // номер накладной
  Номер

  // дата создание накладной
  DateToStr(Дата,'DD mon YYYY г.')

  // номер страницы
  strNN

  // Грузоотправитель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 1, 0)
    + oPrnSoprFun.TTNBelSelfAddr (Грузоотправитель, Грузоотправитель_ЮрАдрес, Грузоотправитель_Адрес, Поставщик, Поставщик_Адрес, Склад, Склад_Адрес, 1)

  // Грузоотправитель - головная организация
  + oPrnSoprFun.GetParentOrg(Грузоотправитель_Головная_Орг_Наимен)

  // Грузополучатель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 2, 0)
    + oPrnSoprFun.TTNBelAddr (Грузополучатель, Грузополучатель_ЮрАдрес, Грузополучатель_Адрес, Поставщик, Поставщик_Адрес, Склад_Пол, Склад_Пол_Адрес, 1)

  // Грузополучатель - головная организация
  + oPrnSoprFun.GetParentOrg(Грузополучатель_Головная_Орг_Наимен)
//******************************************************************************



//******************************************************************************
// СПЕЦИФИКАЦИЯ. ОСНОВНАЯ СТРОКА
//******************************************************************************

  // Графа 1. Наименование товара
  Название_Доп

  // Графа 2. Единица измерения
  ОтпЕд

  // Графа 3. Количество
  DoubleToStr(КолФ,Kol_Pr)

  // Графа 4. Цена
  oPrnSoprFun.GetSPriceMC(спецификацияМЦ_нрек, 0, Валюта_нрек, Признак_МЦ, ЦНацСН, ЦНацБН, ЦНакСН, ЦНакБН)

  // Графа 5. Стоимость
  oPrnSoprFun.GetSSummaMC(0, Признак_МЦ, СтНацСН0, СтНацБН0, СтНакСН, СтНакБН)

  // Графа 6. Ставка НДС
  oPrnSoprFun.GetStavkaNal(ПризнакНДС, ставка_первого_налога)

  // Графа 7. Сумма НДС
  oPrnSoprFun.GetSumNds(0,ПризнакНДС, СтНДСНац0, СтНДСНак)

  // Графа 8. Стоимость с НДС
  oPrnSoprFun.GetSSummaMCsNDS(0, СтНацСН0, СтНакСН)

  // Графа 9. Примечание
  if (isValDoc, sPrimechanieV, sPrimechanie)

#ifdef _TTN_AKTREC_
  + if (isReklNaklNum, oPrnSoprFun.GetNumNaklPrim(спецификацияМЦ_нрек), '')
#end

//******************************************************************************



//******************************************************************************
// МНОГОСТРАНИЧНАЯ НАКЛАДНАЯ. НИЖНИЙ КОЛОНТИТУЛ
//******************************************************************************
  // Итог по странице. Количество.
  if (oPrnSoprFun.IsSumOtpEd(накладная_нрек), DoubleToStr(strОтпЕд, Kol_Pr), ' ')

#ifdef _TTN_DOC_ROZN_
  DoubleToStr (if (isRozn911, if (bNalProd OR bFixGrMC, strАСтНацСБН, strАСтНацБН), strАСтНацСН), Cena_PrR)
#else
  DoubleToStr (if (bNalProd OR bFixGrMC, strАСтНацСБН, strАСтНацБН), Cena_PrR)
#end

  // Итог по странице. Графа 7. Сумма НДС
  DoubleToStr(strАНДСНац, Cena_PrR)

  // Итог по странице. Графа 8. Стоимость с НДС
  DoubleToStr(strАСтНацСН, Cena_PrR)

  // Итог по странице. Сумма НДС прописью
  doubleToString(0, Round(strАНДСНац, GetPrecisionGroup('brForSopr_Price')))

  // Итог по странице. Всего с НДС прописью
  doubleToString(0, Round(strАСтНацСН,GetPrecisionGroup('brForSopr_Price')))

  // Итог по странице. Отпуск разрешил
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность3, отпуск_разрешил3), '')
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность2, отпуск_разрешил2), '')
  oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность, отпуск_разрешил)

  // Итог по странице. Сдал грузоотправитель
  oPrnSoprFun.GetSdalOtp(сдал_отправитель_должность, сдал_отправитель)

  // Итог по странице. Товар к перевозке принял
  oPrnSoprFun.GetTovPrin(ФИОДовер, Экспедитор, ДовЛицоДолжность)

  // Итог по странице. Дата доверенность
  oPrnSoprFun.GetNumAndDateDover(НомерДовер, ДатаДовер)

  // Итог по странице. Организация выдавшая доверенность
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 6, 0)

  // Итог по странице. Принял грузополучатель
  oPrnSoprFun.GetPrGrpol(накладная_нрек)

//******************************************************************************

 ChrBreakPage


//******************************************************************************
// МНОГОСТРАНИЧНАЯ НАКЛАДНАЯ. НИЖНИЙ КОЛОНТИТУЛ
//******************************************************************************
  // Итог по странице. Количество.
  if (oPrnSoprFun.IsSumOtpEd(накладная_нрек), DoubleToStr(strОтпЕд, Kol_Pr), ' ')

  // Итог по странице. Графа 5. Стоимость. Для розничных накладных стоимость с налогами.
#ifdef _TTN_DOC_ROZN_
  DoubleToStr (if (isRozn911, if (bNalProd OR bFixGrMC, strАСтНацСБН, strАСтНацБН), strАСтНацСН), Cena_PrR)
#else
  DoubleToStr (if (bNalProd OR bFixGrMC, strАСтНацСБН, strАСтНацБН), Cena_PrR)
#end

  // Итог по странице. Графа 7. Сумма НДС
  DoubleToStr(strАНДСНац, Cena_PrR)

  // Итог по странице. Графа 8. Стоимость с НДС
  DoubleToStr(strАСтНацСН, Cena_PrR)

  // Итог по странице. Сумма НДС прописью
  DoubleToString(0, Round(strАНДСНац, GetPrecisionGroup('brForSopr_Price')))

  // Итог по странице. Всего с НДС прописью
  DoubleToString(0, Round(strАСтНацСН,GetPrecisionGroup('brForSopr_Price')))

  // Итог по странице. Отпуск разрешил
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность3, отпуск_разрешил3), '')
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность2, отпуск_разрешил2), '')
  oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность, отпуск_разрешил)

  // Итог по странице. Сдал грузоотправитель
  oPrnSoprFun.GetSdalOtp(сдал_отправитель_должность, сдал_отправитель)

  // Итог по странице. Товар к перевозке принял
  oPrnSoprFun.GetTovPrin(ФИОДовер, Экспедитор, ДовЛицоДолжность)

  // Итог по странице. Дата доверенность
  oPrnSoprFun.GetNumAndDateDover(НомерДовер, ДатаДовер)

  // Итог по странице. Организация выдавшая доверенность
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 6, 0)

  // Итог по странице. Принял грузополучатель
  oPrnSoprFun.GetPrGrpol(накладная_нрек)

//******************************************************************************
  ChrBreakPage




//******************************************************************************
// МНОГОСТРАНИЧНАЯ НАКЛАДНАЯ. ИТОГО ПО НАКЛАДНОЙ
//******************************************************************************
 // Итоговый лист. Количество.
 if (oPrnSoprFun.IsSumOtpEd(накладная_нрек), DoubleToStr(sumОтпЕд, Kol_Pr), ' ')

#ifdef _TTN_DOC_ROZN_
 DoubleToStr (if (isRozn911, if (bNalProd OR bFixGrMC, АСтНацСБН, АСтНацБН), АСтНацСН), Cena_PrR)
#else
 DoubleToStr (if (bNalProd OR bFixGrMC, АСтНацСБН, АСтНацБН), Cena_PrR)
#end

  // Итог по накладной. Графа 7. Сумма НДС
  DoubleToStr(АНДСНац, Cena_PrR)

  // Итог по накладной. Графа 8. Стоимость с НДС
  DoubleToStr(АСтНацСН, Cena_PrR)

//******************************************************************************



//******************************************************************************
// МНОГОСТРАНИЧНАЯ НАКЛАДНАЯ. ИТОГОВЫЙ ЛИСТ
//******************************************************************************

  // Шапка накладной
  // Грузоотправитель - код УНП
  oPrnSoprFun.GetOrgUNN(Грузоотправитель, ГрузоотправительУНН, ПоставщикУНН)

  // Грузополучатель - код УНП
  oPrnSoprFun.GetOrgUNN(Грузополучатель, ГрузополучательУНН, ПоставщикУНН)

  // Дата создания накладной - день
  ДатаДень

  // Дата создания накладной - месяц
  ДатаМесяц

  // Дата создания накладной - год
  ДатаГод

  // Номер накладной
  Номер

  // Грузоотправитель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 1, 0)
    + oPrnSoprFun.TTNBelSelfAddr (Грузоотправитель, Грузоотправитель_ЮрАдрес, Грузоотправитель_Адрес, Поставщик, Поставщик_Адрес, Склад, Склад_Адрес, 1)

  // Грузоотправитель - наименование головной организации
#ifndef _TTN_2009F_
  + ' ' +
#end
  oPrnSoprFun.GetParentOrg(Грузоотправитель_Головная_Орг_Наимен)

  // Грузополучатель
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 2, 0)
    + oPrnSoprFun.TTNBelAddr (Грузополучатель, Грузополучатель_ЮрАдрес, Грузополучатель_Адрес, Поставщик, Поставщик_Адрес, Склад_Пол, Склад_Пол_Адрес, 1)

  // Грузополучатель - наименование головной организации
#ifndef _TTN_2009F_
  + ' ' +
#end
  oPrnSoprFun.GetParentOrg(Грузополучатель_Головная_Орг_Наимен)

  // Основание отпуска
  oPrnSoprFun.GetOsnovOtp(Дата_Договора, ДатаОснования, Номер_Договора, Основание, Основание_отпуска, Цель_приобретения)

  // Итоговый лист. Номер накладной
  Номер

  // Итоговый лист. Дата накладной
  DateToStr(Дата,'DD mon YYYY г.')

  // Итоговый лист. Число страниц
  strNN

  // Итоговый лист. Количество.
  if (oPrnSoprFun.IsSumOtpEd(накладная_нрек), DoubleToStr(sumОтпЕд, Kol_Pr), ' ')

  // Итоговый лист. Графа 5. Стоимость. Для розничных накладных стоимость с налогами
#ifdef _TTN_DOC_ROZN_
  DoubleToStr (if (isRozn911, if (bNalProd OR bFixGrMC, АСтНацСБН, АСтНацБН), АСтНацСН), Cena_PrR)
#else
  DoubleToStr (if (bNalProd OR bFixGrMC, АСтНацСБН, АСтНацБН), Cena_PrR)
#end

  // Итоговый лист. Графа 7. Сумма НДС
  DoubleToStr(АНДСНац, Cena_PrR)

  // Итоговый лист. Графа 8. Стоимость с НДС
  DoubleToStr(АСтНацСН, Cena_PrR)

  // Итоговый лист. Сумма НДС прописью
  DoubleToString(0, Round(АНДСНац,GetPrecisionGroup('brForSopr_Price')))

  // Итоговый лист. Всего с НДС прописью
  DoubleToString(0, Round(АСтНацСН,GetPrecisionGroup('brForSopr_Price')))

  // Итоговый лист. Отпуск разрешил
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность3, отпуск_разрешил3), '')
  if (DopOtpRaz, oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность2, отпуск_разрешил2), '')
  oPrnSoprFun.GetOtpRaz(отпуск_разрешил_должность, отпуск_разрешил)

  // Итог по накладной. Сдал грузоотправитель
  oPrnSoprFun.GetSdalOtp(сдал_отправитель_должность, сдал_отправитель)

  // Итог по накладной. Товар к перевозке принял
  oPrnSoprFun.GetTovPrin(ФИОДовер, Экспедитор, ДовЛицоДолжность)

  // Итог по накладной. Дата доверенность
  oPrnSoprFun.GetNumAndDateDover(НомерДовер, ДатаДовер)

  // Итог по накладной. Организация выдавшая доверенность
  oPrnSoprFun.GetOrgFullName(накладная_нрек, 6, 0)

  // Итог по странице. Принял грузополучатель
  oPrnSoprFun.GetPrGrpol(накладная_нрек)

  // С товаром переданы документы. Документы, поставляемые с грузом. 1-я строка.
  if (bDocEveryStr, '', все_документы1)

  // С товаром переданы документы. Сертификаты партий. 1-я строка.
  if (все_сертификаты1 = '', '', ' Сертификаты: ' + все_сертификаты1) +
    if (ПрейскурантЦен  = '', '', ' Прейскурант: ' + Trim(ПрейскурантЦен))

  if (все_прайслисты = '', '', все_прайслисты)

  ChrBreakPage
.endfields
#include ttn_098.frn
.{
#include sum_calc.frn
#include ttn_095.frn
#include ttn_init_09.frn
.begin
!для fast-отчетов
  Define_TTN_2009f := true;
  var Options: word;
  readmydsk(Options,'OptionsTN2',false);
  ChrBreakPage := if (Options = 1, '', '');
 end.
.if tovn01
.end
.if tovn02
.end
.{?Internal;not CheckKol
.begin
 PrnOK:=true;
 end.
 Ш
                                                        Грузоотправитель                 Грузополучатель
                                                 ┌──────────────────────────────┬──────────────────────────────────┐
                                              УНП│ @@@@@@@@@@@@@@@@@@@@@@@@@@@@ │ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ │
                                                 └──────────────────────────────┴──────────────────────────────────┘




                                                                "@@" @@@@@@@@ 20@@ г.                        @@@@@@@@@@@@@@@@@@@

Грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (наименование, адрес)
Грузополучатель   Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (наименование, адрес)
Основание отпуска  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                    (дата и номер договора или другого документа)

                                                                 I. ТОВАРНЫЙ РАЗДЕЛ
┌────────────────────────────────┬───────────────┬──────────────┬────────────┬──────────────┬───────┬────────────┬───────────────┬─────────────────────────┐
│        Наименование товара     │    Единица    │  Количество  │    Цена,   │  Стоимость   │ Ставка│   Сумма    │   Стоимость   │        Примечание       │
│                                │   измерения   │              │    руб.    │  продукции,  │  НДС, │    НДС,    │    с НДС,     │                         │
│                                │               │              │            │     руб.     │   %   │    руб.    │     руб.      │                         │
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│               1                │       2       │       3      │      4     │      5       │   6   │     7      │       8       │            9            │
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
.}
.{
!.[h
.{?Internal;CheckKol and Kounter=0
.begin
  strNN       := strNN + 1;
  strАСтНацБН := 0;
  strАНДСНац  := 0;
  strАСтНацСН := 0;
  strАСтНацСБН:= 0;
  strАКолФ    := 0;
  strОтпЕд    := 0;

  if not readmydsk(fixkol,'KolLineInTN2'+string(strNN),false)
    readmydsk(FixKol,'FixKolTN2',false)
 End.
.begin
 PrnOK:=true;
 end.
Приложение к накладной № Ж@@@@@@@@@@@@ Ж от  Ж@@@@@@@@@@@@@@@@@@@@ Ж                                                                                 страница @@@@
Грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
Грузополучатель   Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
┌────────────────────────────────┬───────────────┬──────────────┬────────────┬──────────────┬───────┬────────────┬───────────────┬─────────────────────────┐
│        Наименование товара     │    Единица    │  Количество  │    Цена,   │  Стоимость   │ Ставка│   Сумма    │    Всего с    │        Примечание       │
│                                │   измерения   │              │    руб.    │  продукции,  │  НДС, │    НДС,    │      НДС,     │                         │
│                                │               │              │            │     руб.     │   %   │    руб.    │      руб.     │                         │
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│               1                │       2       │       3      │      4     │      5       │   6   │     7      │       8       │            9            │
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
.}
!.]h
.{?Internal;PrnOK
#include ttn_fast.frn
#include ttn_097.frn
#include ttn_096.frn
│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&&&│&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@│
.}
.{?Internal;CheckKol and kounter=fixkol and PrnOK
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│Итого                           │       X       │&&&&&&&&&&&&&&│      X     │&&&&&&&&&&&&&&│   X   │&&&&&&&&&&&&│&&&&&&&&&&&&&&&│                         │
└────────────────────────────────┴───────────────┴──────────────┴────────────┴──────────────┴───────┴────────────┴───────────────┴─────────────────────────┘

Всего сумма НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
Всего стоимость с НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
.{?Internal;DopOtpRaz
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
.}
Отпуск разрешил  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                (должность, фамилия, инициалы, подпись)
Сдал грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                  (должность, фамилия, инициалы, подпись; штамп(печать) грузоотправителя)
Товар к доставке принял  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                  (должность, фамилия, инициалы, подпись)
по доверенности  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж  выданной  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                           (номер, дата)                                                          (наименование организации)
Принял грузополучатель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                   (должность, фамилия, инициалы, подпись; штамп(печать) грузополучателя)
^
.begin kounter:=0 end.
.begin
 PrnOK:=false;
 end.
.}
.}
.{?Internal;CheckKol and kounter<>fixkol and PrnOK
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│Итого                           │       X       │&&&&&&&&&&&&&&│      X     │&&&&&&&&&&&&&&│   X   │&&&&&&&&&&&&│&&&&&&&&&&&&&&&│                         │
└────────────────────────────────┴───────────────┴──────────────┴────────────┴──────────────┴───────┴────────────┴───────────────┴─────────────────────────┘

Всего сумма НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
Всего стоимость с НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
.{?Internal;DopOtpRaz
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
.}
Отпуск разрешил  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                (должность, фамилия, инициалы, подпись)
Сдал грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                  (должность, фамилия, инициалы, подпись; штамп(печать) грузоотправителя)
Товар к доставке принял  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                  (должность, фамилия, инициалы, подпись)
по доверенности  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж  выданной  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                           (номер, дата)                                                          (наименование организации)
Принял грузополучатель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                   (должность, фамилия, инициалы, подпись; штамп(печать) грузополучателя)
^
.begin kounter:=0 end.
.begin
 PrnOK:=false;
 end.
.}
.{?Internal;not CheckKol and PrnOK
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│Итого                           │       X       │&&&&&&&&&&&&&&│      X     │&&&&&&&&&&&&&&│   X   │&&&&&&&&&&&&│&&&&&&&&&&&&&&&│                         │
└────────────────────────────────┴───────────────┴──────────────┴────────────┴──────────────┴───────┴────────────┴───────────────┴─────────────────────────┘
.begin
 PrnOK:=false;
 end.
.}
.{?Internal;CheckKol
                                                        Грузоотправитель                 Грузополучатель
                                                 ┌──────────────────────────────┬──────────────────────────────────┐
                                              УНП│ @@@@@@@@@@@@@@@@@@@@@@@@@@@@ │ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ │
                                                 └──────────────────────────────┴──────────────────────────────────┘




                                                                "@@" @@@@@@@@ 20@@ г.                        @@@@@@@@@@@@@@@@@@@

Грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (наименование, адрес)
Грузополучатель   Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (наименование, адрес)
Основание отпуска  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                    (дата и номер договора или другого документа)

                                                                 I. ТОВАРНЫЙ РАЗДЕЛ
┌────────────────────────────────┬───────────────┬──────────────┬────────────┬──────────────┬───────┬────────────┬───────────────┬─────────────────────────┐
│        Наименование товара     │    Единица    │  Количество  │    Цена,   │  Стоимость   │ Ставка│   Сумма    │    Всего с    │        Примечание       │
│                                │   измерения   │              │    руб.    │  продукции,  │  НДС, │    НДС,    │      НДС,     │                         │
│                                │               │              │            │     руб.     │   %   │    руб.    │      руб.     │                         │
├────────────────────────────────┼───────────────┼──────────────┼────────────┼──────────────┼───────┼────────────┼───────────────┼─────────────────────────┤
│               1                │       2       │       3      │      4     │      5       │   6   │     7      │       8       │            9            │
├────────────────────────────────┴───────────────┴──────────────┴────────────┴──────────────┴───────┴────────────┴───────────────┴─────────────────────────┤
│Перечень продукции согласно приложению к накладной № @@@@@@@@@@@@  от @@@@@@@@@@@@@@@@@@@@  на @@@@ страницах.                                            │
├────────────────────────────────┬───────────────┬──────────────┬────────────┬──────────────┬───────┬────────────┬───────────────┬─────────────────────────┤
│Итого                           │       X       │&&&&&&&&&&&&&&│      X     │&&&&&&&&&&&&&&│   X   │&&&&&&&&&&&&│&&&&&&&&&&&&&&&│                         │
└────────────────────────────────┴───────────────┴──────────────┴────────────┴──────────────┴───────┴────────────┴───────────────┴─────────────────────────┘
.}
.{ CHECKENTER TOVNUSL
.}

Всего сумма НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
Всего стоимость с НДС  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                            (прописью)
.{?Internal;DopOtpRaz
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
.}
Отпуск разрешил  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                (должность, фамилия, инициалы, подпись)
Сдал грузоотправитель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                  (должность, фамилия, инициалы, подпись; штамп(печать) грузоотправителя)
Товар к доставке принял  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                                  (должность, фамилия, инициалы, подпись)
по доверенности  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж  выданной  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                           (номер, дата)                                                          (наименование организации)
Принял грузополучатель  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
                                                   (должность, фамилия, инициалы, подпись; штамп(печать) грузополучателя)
С товаром переданы документы  Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
 Ж@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ж
.{
.}
#ifdef __SKYSHOP__
#include sky.inc
#end
^ Ш
.}
.endform
#end
#end // _CURRENT_FORM_
#undef _TN2_PC_FRN_
